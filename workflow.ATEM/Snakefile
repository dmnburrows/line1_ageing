configfile:
    "config.json"

#==============================
rule all:
#==============================
    input:
        count_mat=config['path']+"/prac.2/ATEM_counts.csv",
        merge_bam=config['path']+"/prac.2/5pfilt-tss.bam",
        log_complete=config['path']+"/prac.2/log.complete.npy"        


#Count total reads in bam file
#==============================
rule count_reads:
#==============================
    input:
        config['path']+"/prac.2/Aligned.sortedByCoord.out.bam"
    output:
        config['path']+"/prac.2/total_reads.txt"        
    shell:
        "samtools view {input} | wc -l > {output}"

#Split bam into forward and reverse strands
#==========================================
rule split_reads:
#==========================================
    input:
        config['path']+"/prac.2/Aligned.sortedByCoord.out.bam"
    output:
        plus=config['path']+"/prac.2/plus.bam",
        minus=config['path']+"/prac.2/minus.bam"
    run:
        shell("samtools view -h -F 0x10 {input} > {output.plus}"),
        shell("samtools view -h -f 0x10 {input} > {output.minus}")


#Filter bam files with TE bed file using samtools
#==========================================
rule samfilter_reads:
#==========================================
    input:
        plus=config['path']+"/prac.2/plus.bam",
        minus=config['path']+"/prac.2/minus.bam"
    output:
        plus=config['path']+"/prac.2/plus.5pfilt.bam",
        minus=config['path']+"/prac.2/minus.5pfilt.bam"
    run:
        shell("samtools view -b -h -L "+config['bed_plus_path']+" {input.plus} > {output.plus}")   
        shell("samtools index {output.plus}")
        shell("samtools view -b -h -L "+config['bed_minus_path']+" {input.minus} > {output.minus}")
        shell("samtools index {output.minus}")

# Obtain metadata from filtered bam files for remapping after ATEM
#============================================
rule get_metadata:
#============================================
    input:
        plus=config['path']+"/prac.2/plus.5pfilt.bam",
        minus=config['path']+"/prac.2/minus.5pfilt.bam"
    output:
        plus=config['path']+"/prac.2/plus.5pfilt.metadata.txt",
        minus=config['path']+"/prac.2/minus.5pfilt.metadata.txt"
    run:
        shell("samtools view {input.plus} | cut -f -9 > {output.plus} ")
        shell("samtools view {input.minus} | cut -f -9 > {output.minus} ")

#Active Transposable Element Mapping 
# This rule will run the ATEM pipeline which returns counts of putatively 
# active TEs based on 5'alignment at 5' end
#============================================
rule ATEM:
#============================================
    input:
        bam_pl=config['path']+"/prac.2/plus.5pfilt.bam",
        bam_mi=config['path']+"/prac.2/minus.5pfilt.bam",
        meta_pl=config['path']+"/prac.2/plus.5pfilt.metadata.txt",
        meta_mi=config['path']+"/prac.2/minus.5pfilt.metadata.txt",
        n_reads=config['path']+"/prac.2/total_reads.txt"        
    output:
        count_mat=config['path']+"/prac.2/ATEM_counts.csv",
        meta_pl_notin=config['path']+"/prac.2/plus.5pfilt-notin.metadata.txt",
        meta_mi_notin=config['path']+"/prac.2/minus.5pfilt-notin.metadata.txt",
        bam_ind=config['path']+"/prac.2/5pfilt-tss_splitstrand.index.npy"

    conda:
        "ATEM.yaml"
    script:
        "ATEM.py"

#Filter 5p filt bam file by ATEM-based filtering to 
#return sam file with only 5p filt at tss reads
#============================================
rule filter_bam_by_ATEM:
#============================================
    input:
        plus=config['path']+"/prac.2/plus.5pfilt.bam",
        minus=config['path']+"/prac.2/minus.5pfilt.bam",
        meta_pl_notin=config['path']+"/prac.2/plus.5pfilt-notin.metadata.txt",
        meta_mi_notin=config['path']+"/prac.2/minus.5pfilt-notin.metadata.txt"

    output:
        plus_sam=config['path']+"/prac.2/plus.5pfilt-tss.sam",
        minus_sam=config['path']+"/prac.2/minus.5pfilt-tss.sam"

    shell:
        """
        samtools view -h {input.plus} | awk 'NR==FNR{{a[$1"\t"$2"\t"$3"\t"$4"\t"$5"\t"$6"\t"$7"\t"$8"\t"$9];next}} !($1"\t"$2"\t"$3"\t"$4"\t"$5"\t"$6"\t"$7"\t"$8"\t"$9 in a)' {input.meta_pl_notin} -> {output.plus_sam}
        samtools view -h {input.minus} | awk 'NR==FNR{{a[$1"\t"$2"\t"$3"\t"$4"\t"$5"\t"$6"\t"$7"\t"$8"\t"$9];next}} !($1"\t"$2"\t"$3"\t"$4"\t"$5"\t"$6"\t"$7"\t"$8"\t"$9 in a)' {input.meta_mi_notin} -> {output.minus_sam}
        """


#============================================
rule sam_to_bam:
#============================================
    input:
        plus_sam=config['path']+"/prac.2/plus.5pfilt-tss.sam",
        minus_sam=config['path']+"/prac.2/minus.5pfilt-tss.sam"
    output:
        plus_bam=config['path']+"/prac.2/plus.5pfilt-tss.bam",
        minus_bam=config['path']+"/prac.2/minus.5pfilt-tss.bam"
    run:
        shell("samtools view -S -b {input.plus_sam} > {output.plus_bam}")
        shell("samtools view -S -b {input.minus_sam} > {output.minus_bam}")


#============================================
rule strand_merge_and_index:
#============================================
    input:
        plus_bam=config['path']+"/prac.2/plus.5pfilt-tss.bam",
        minus_bam=config['path']+"/prac.2/minus.5pfilt-tss.bam"
    output:
        merge_bam=config['path']+"/prac.2/5pfilt-tss.bam"
    run:
        shell("samtools merge -f {output.merge_bam} {input.plus_bam} {input.minus_bam}")
        shell("samtools index {output.merge_bam}")


#============================================
rule all_reads_accounted_for:
#============================================
    input: 
        bam_ind=config['path']+"/prac.2/5pfilt-tss_splitstrand.index.npy",
        merged_bam=config['path']+"/prac.2/5pfilt-tss.bam",
        bam_pl=config['path']+"/prac.2/plus.5pfilt.bam",
        bam_mi=config['path']+"/prac.2/minus.5pfilt.bam"
    output:
        log_complete=config['path']+"/prac.2/log.complete.npy"        
    conda:
        "ATEM.yaml"
    script:
        "sanity_check.py"